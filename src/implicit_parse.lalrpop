use common::Id;
use common::Op2::*;
use common::Const::*;
use implicit::Expr;
use implicit::Expr::*;
use tok::{self, Tok};

#[LALR]
grammar<'input>(text: &'input str);

pub Program: Expr = {
    Expr,
};

Expr: Expr = {
    "if" <c:Expr> "then" <t:Expr> "else" <f:Expr>    => If(Box::new(c), Box::new(t), Box::new(f)),
    "let" <id:Ident> "=" <v:Expr> "in" <e:Expr>      => Let(id, Box::new(v), Box::new(e)),
    "let" "rec" <f:Ident> "=" <v:Expr> "in" <e:Expr> => Let(f.clone(), Box::new(Fix(f, v)), Box::new(e)),
    "fun" <id:Ident> "->" <e:Expr>                   => Fun(id, Box::new(e)),
    "fix" <id:Ident> "->" <e:Expr>                   => Fix(id, Box::new(e)),
    "begin" <e:Expr> "end"                           => e,
    LCmp,
};

LCmp: Expr = {
    <l:ACmp> "∧" <r:LCmp> => Op2(And, Box::new(l), Box::new(r)),
    <l:ACmp> "∨" <r:LCmp> => Op2(Or, Box::new(l), Box::new(r)),
    ACmp,
};

ACmp: Expr = {
    <l:Add> "=" <r:ACmp>  => Op2(Eq, Box::new(l), Box::new(r)),
    <l:Add> "<" <r:ACmp>  => Op2(LT, Box::new(l), Box::new(r)),
    <l:Add> "<=" <r:ACmp> => Op2(LTE, Box::new(l), Box::new(r)),
    <l:Add> ">" <r:ACmp>  => Op2(GT, Box::new(l), Box::new(r)),
    <l:Add> ">=" <r:ACmp> => Op2(GTE, Box::new(l), Box::new(r)),
    Add,
};

Add: Expr = {
    <l:Add> "-" <r:Mul> => Op2(Sub, Box::new(l), Box::new(r)),
    <l:Add> "+" <r:Mul> => Op2(Add, Box::new(l), Box::new(r)),
    Mul,
};

Mul: Expr = {
    <l:Mul> "*" <r:Get> => Op2(Mul, Box::new(l), Box::new(r)),
    Get,
};

Get: Expr = {
    <id:Ident> "[" <idx:Add> "]" "<-" <e:Atom> => SetArray(Box::new(Var(id)), Box::new(idx), Box::new(e)),
    <id:Ident> "[" <idx:Add> "]"               => GetArray(Box::new(Var(id)), Box::new(idx)),
    App,
};

App: Expr = {
    "array" "(" <sz:Add> "," <n:Add> ")" => MkArray(Box::new(sz), Box::new(n)),
    "!" <a:Atom>                         => App(Box::new(Var(String::from("not"))), Box::new(a)),
    <a:App> <b:Atom>                     => App(Box::new(a), Box::new(b)),
    Atom,
};

Atom: Expr = {
    <id:Ident>    => Var(id),
    "ν"           => V,
    "★"           => Star,
    "true"        => Const(Bool(true)),
    "false"       => Const(Bool(true)),
    "(" <Exprs> ")",
    Int,
};

Exprs: Expr = {
    Expr,
};

Int: Expr = {
    Num => Const(Int(<>)),
};

Ident: Id = {
    <id:"Iden"> => String::from(id),
};

Num: i64 = {
    "Num",
};

extern {
    type Location = usize;
    type Error = tok::Error;
    enum Tok<'input> {
        "if" => Tok::If,
        "then" => Tok::Then,
        "else" => Tok::Else,
        "begin" => Tok::Begin,
        "end" => Tok::End,
        "let" => Tok::Let,
        "in" => Tok::In,
        "," => Tok::Comma,
        "=" => Tok::Eq,
        "->" => Tok::RArrow,
        "<-" => Tok::LArrow,
        "<" => Tok::Lt,
        "<=" => Tok::Lte,
        ">" => Tok::Gt,
        ">=" => Tok::Gte,
        "+" => Tok::Plus,
        "-" => Tok::Minus,
        "*" => Tok::Mul,
        "!" => Tok::Exclaim,
        "::" => Tok::Cons,
        "(" => Tok::LParen,
        ")" => Tok::RParen,
        "[" => Tok::LBracket,
        "]" => Tok::RBracket,
        "fun" => Tok::Fun,
        "fix" => Tok::Fix,
        "head" => Tok::Head,
        "tail" => Tok::Tail,
        "empty?" => Tok::Emptyq,
        "true" => Tok::True,
        "false" => Tok::False,
        "empty" => Tok::Empty,
        "array" => Tok::Array,
        "set" => Tok::Set,
        "rec" => Tok::Rec,
        "ν" => Tok::V,
        "★" => Tok::Star,
        "∧" => Tok::And,
        "∨" => Tok::Or,

        "Iden" => Tok::Ident(<&'input str>),
        "Num" => Tok::Num(<i64>),
    }
}
